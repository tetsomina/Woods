# Maintainer: Cullen Ross
_pkgname=rxvt-unicode
pkgname=rxvt-unicode-barbarossa
pkgver=9.26
pkgrel=1
pkgdesc='Unicode enabled rxvt-clone terminal emulator (urxvt) with true color, enhanced glyphs and improved font rendering support'
arch=('i686' 'x86_64')
url='http://software.schmorp.de/pkg/rxvt-unicode.html'
license=('GPL')
depends=(
    'libxft'
    'libxt'
    'perl'
    'rxvt-unicode-terminfo'
)
optdepends=('gtk2-perl: to use the urxvt-tabbed')
provides=(
    'rxvt-unicode'
)
conflicts=(
    'rxvt-unicode'
)
source=(
    http://dist.schmorp.de/rxvt-unicode/$_pkgname-$pkgver.tar.bz2
    'urxvt.desktop'
    'urxvtc.desktop'
    'urxvt-tabbed.desktop'
    '24-bit-color.patch'
    'enable-wide-glyphs.patch'
    'improve-font-rendering.patch'
    '256color.patch'
    'matcher-vi-bindings.patch'
    'newterm'
    'noinc.patch'
    'secondaryWheel.patch'
    'add-space-to-extent_test_chars.patch'
)
sha1sums=('d325d8cdea6bcb8e0b8b219b8451bf5c690b6c62'
          'b5a4507f85ebb7bac589db2e07d9bc40106720d9'
          '62c4ffecfce6967def394dd4d418b68652372ea2'
          'cd204d608d114d39c80331efe0af0231ad6b7e18'
          '560097c0d6377461fcbe4c109f3113a6bcb38982'
          'c5ee4a50902d8c8d278938b080464a16d2c6af56'
          '772b62071d73c0021adf334f037b95ef13d34317'
          'a3bc610f82cc3f4d2ceb7471a8d419f307d1d908'
          'eb11ce847839d9764b7883d45d59c5fbdcae5962'
          '362e06e59b0d8886ebdf697d138526875e5bb74f'
          '3ecb8d6c4d52e4445b797e9470ab84483a58b953'
          'e50503f786deb6d6c2b6b8bcf486bf509c14af71'
          '69b77c0b4b4587117f3a6e240a5bd6389ed40be3')

prepare() {
    cd $_pkgname-$pkgver

    # The repo with original 24-bit-color.patch is no longer available:
    # https://gist.githubusercontent.com/dan-santana/63271adf12171e0fc0bc/raw/70c6343d1c0b3bca0aba4f587ed501e6cbd98d00/24-bit-color.patch
    # I have rewritten the patch to work with version 9.26.
    # If someone has a better solution, please contact me!
    patch -p0 -i ../24-bit-color.patch

    patch -p0 -i ../matcher-vi-bindings.patch
    patch -p0 -i ../256color.patch
    #patch -p0 -i ../noinc.patch

    patch -p1 -i ../add-space-to-extent_test_chars.patch
    # If you have problems with character rendering, try installing libxft-bgra from aur.
    # https://aur.archlinux.org/packages/rxvt-unicode-cvs-patched-wideglyphs
    patch -p0 -i ../enable-wide-glyphs.patch

    # https://aur.archlinux.org/packages/rxvt-unicode-improve-font-rendering
    patch -p0 -i ../improve-font-rendering.patch

    patch -p1 -i ../secondaryWheel.patch
}

build() {
    cd $_pkgname-$pkgver

    # do not specify --with-terminfo (FS#46424)
    ./configure \
        --prefix=/usr \
        --enable-24-bit-color \
        --enable-256-color \
        --enable-combining \
        --disable-fading \
        --disable-fallback \
        --enable-font-styles \
        --enable-frills \
        --disable-iso14755 \
        --disable-keepscrolling \
        --enable-lastlog \
        --enable-mousewheel \
        --disable-next-scroll \
        --enable-perl \
        --disable-pixbuf \
        --disable-pointer-blank \
        --disable-rxvt-scroll \
        --enable-selectionscrolling \
        --enable-slipwheeling \
        --disable-smart-resize \
        --disable-startup-notification \
        --enable-text-blink \
        --disable-transparency \
        --enable-unicode3 \
        --enable-utmp \
        --enable-wide-glyphs \
        --enable-wtmp \
        --enable-xft \
        --enable-xim \
        --disable-xterm-scroll
    make
}

package() {
    # install freedesktop menu
    for _f in urxvt urxvtc urxvt-tabbed; do
        install -Dm 644 $_f.desktop "$pkgdir/usr/share/applications/$_f.desktop"
    done

    # install perl script newterm (https://github.com/effigies/urxvt-perl)
    install -Dm 644 newterm "$pkgdir/usr/lib/urxvt/perl/newterm"

    cd $_pkgname-$pkgver


    # workaround terminfo installation
    export TERMINFO="$srcdir/terminfo"
    install -d "$TERMINFO"
    make DESTDIR="$pkgdir" install

    # install the tabbing wrapper (requires gtk2-perl)
    sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
    install -Dm 755 doc/rxvt-tabbed "$pkgdir/usr/bin/urxvt-tabbed"
}
